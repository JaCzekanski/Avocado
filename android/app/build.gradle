apply plugin: 'com.android.application'

def signingPassword = System.getenv("keystore_password")
def signingEnabled = signingPassword != null
println "App signing: $signingEnabled"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "info.czekanski.avocado"
        minSdkVersion 24
        targetSdkVersion 28
        versionCode 100
        versionName "0.1.0"
        externalNativeBuild {
            ndkBuild {
                arguments 'PM5_CONFIG=release_arm', '-j4'
                targets "avocado"
            }
        }
    }
    signingConfigs {
        if (signingEnabled) {
            release {
                storeFile = file("../avocado.keystore")
                keyAlias = "avocado"
                storePassword = signingPassword
                keyPassword = signingPassword
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (signingEnabled) {
                signingConfig signingConfigs.release
            }
            ndk {
                abiFilters "arm64-v8a", "armeabi-v7a"
            }
        }
        debug {
            ndk {
                abiFilters "arm64-v8a", "armeabi-v7a", "x86_64"
            }
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'jni/Android.mk'
        }
    }

    compileOptions {
        encoding = 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
}
